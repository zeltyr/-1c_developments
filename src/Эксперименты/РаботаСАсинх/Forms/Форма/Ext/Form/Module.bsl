#Область ОписаниеПеременных

&НаКлиенте
Перем ВопросПриЗакрытииЗадан;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПутьКПапкеСФайлами = "C:\Program Files\1cv8\conf";
	ПутьКФайлу = "C:\Program Files\1cv8\conf\conf.cfg";
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПриОткрытииАсинх(Отказ)
	
	Отказ = Истина;
	ВызватьИсключение "Я плохой обработчик";
	МатОшибка = 1 / 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииВопрос(Отказ)
	
	ЗадатьВопросАсинх_ПриОткрытии();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	//Если НЕ ВопросПриЗакрытииЗадан Тогда
	//	ЗадатьВопросАсинх_ПриЗакрытии();
	//	Отказ = Истина;
	//КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Демо_АнализОбещания(Команда)
	
	Обещание = ПредупреждениеАсинх("Ну почему ты не купил слона?");
	ВывестиПараметрыПеременной(Обещание);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура Демо_ЗадатьВопросАсинх(Команда)
	
	обещание = ВопросАсинх("Купить слона?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Результат = Ждать обещание; 
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		текст = НСтр("ru='Ответ утвержительный!'");
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда 
		текст = НСтр("ru='Ответ отрицательный!'");
	Иначе
		текст = НСтр("ru='Ответ не получен!'");
	КонецЕсли;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = текст;
	Сообщение.Сообщить();
	
	ВывестиПараметрыПеременной(Результат);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура Демо_ПоказатьПредупреждениеАсинх(Команда)
	
	текст = НСтр("ru='Это асинхронное предупреждение!'");	
	
	Обещание = ПредупреждениеАсинх(текст);
	// тут выполнение остановиться пока пользователь не закроет предупреждение. Второе предупреждение показано не будет
	Результат = Ждать Обещание;
	
	// предупреждение выведется и, но код пойдёт испоняться дальше
	текст = НСтр("ru='Это второе асинхронное предупреждение!'");	
	ПредупреждениеАсинх(текст);

	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = НСтр("ru='текст после второго предупреждения!'");;
	Сообщение.Сообщить();
		
	ВывестиПараметрыПеременной(Результат);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура Демо_ОткрытьЗначениеАсинх(Команда)
	
	тестовыйСписок = Новый СписокЗначений;
	тестовыйСписок.Добавить("тест1", "тестовое значение 1");
	тестовыйСписок.Добавить("тест2", "тестовое значение 2");
	тестовыйСписок.Добавить("тест3", "тестовое значение 3");
	
	Обещание = ОткрытьЗначениеАсинх(тестовыйСписок);
	Результат = Ждать Обещание; // тут выполнение остановиться пока список не закроется
	
	ВывестиПараметрыПеременной(Результат);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура Демо_ВвестиЧислоАсинх(Команда)
	
	Обещание = ВвестиЧислоАсинх(1, "укажите любое число");
	Результат = Ждать Обещание;
	
	ВывестиПараметрыПеременной(Результат);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура Демо_НайтиФайлыАсинх(Команда)
	
	Обещание = НайтиФайлыАсинх(ПутьКПапкеСФайлами, "*.*", Ложь);
	Результат = Ждать Обещание;
	
	ВывестиПараметрыПеременной(Результат);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура Демо_ПоместитьФайлНаСерверАсинх(Команда)
	
	Обещание = ПоместитьФайлНаСерверАсинх(,,, ПутьКФайлу, ЭтаФорма.УникальныйИдентификатор);	
	Результат = Ждать Обещание;
	
	ВывестиПараметрыПеременной(Результат);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура Демо_ПоместитьФайлыНаСерверАсинх(Команда)

	Обещание = НайтиФайлыАсинх(ПутьКПапкеСФайлами, "*.*", Ложь);
	Результат = Ждать Обещание;
	
	ДобавляемыеФайлы = Новый Массив;
	Для каждого НайденныйФайл из Результат Цикл
		
		ОписаниеДобавляемогоФайла = Новый ОписаниеПередаваемогоФайла;
		ОписаниеДобавляемогоФайла.Имя = НайденныйФайл.ПолноеИмя;
		ДобавляемыеФайлы.Добавить(ОписаниеДобавляемогоФайла);
		
	КонецЦикла;
	
	Обещание = ПоместитьФайлыНаСерверАсинх(,,ДобавляемыеФайлы, ЭтаФорма.УникальныйИдентификатор);	
	Результат = Ждать Обещание;
	
	ВывестиПараметрыПеременной(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Демо_ПоявлениеИсключенияСинх(Команда)
	
	Попытка
		ВызватьОшибку();
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Сообщение.Сообщить();
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура Демо_ПоявлениеИсключенияАсинх(Команда)
	
	Результат = Ждать ВызватьОшибку(); 
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура Демо_ПопыткаИсключениеАсинх(Команда)
	
	Попытка
		Результат = Ждать ВызватьОшибку();
	Исключение
		текст = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = текст;
		Сообщение.Сообщить();
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти
 
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВывестиПараметрыПеременной(ЗНАЧ ПеременнаяДляАнализа)
	
	Сообщение = Новый СообщениеПользователю;
	
	Если ТипЗнч(ПеременнаяДляАнализа) = Тип("Файл") Тогда
		текст = НСтр("ru='Значение переменной: %1, тип: %2, имя файла: %3!'"); 
		текст = СтрШаблон(текст, ПеременнаяДляАнализа, ТипЗнч(ПеременнаяДляАнализа), ПеременнаяДляАнализа.Имя);
	Иначе	
		текст = НСтр("ru='Значение переменной: %1, тип: %2!'"); 
		текст = СтрШаблон(текст, ПеременнаяДляАнализа, ТипЗнч(ПеременнаяДляАнализа));
	КонецЕсли;
	Сообщение.Текст = текст;
	Сообщение.Сообщить(); 
	
	Если ТипЗнч(ПеременнаяДляАнализа) = Тип("ОписаниеПомещенногоФайла") Тогда
		
		Сообщение.Текст = СтрШаблон("Полный путь к файлу: %1", ПеременнаяДляАнализа.СсылкаНаФайл.Файл.ПолноеИмя);
		Сообщение.Сообщить();

		Сообщение.Текст = СтрШаблон("Адрес: %1", ПеременнаяДляАнализа.Адрес);
		Сообщение.Сообщить();
		
		Сообщение.Текст = СтрШаблон("Помещение файла отменено: %1", ПеременнаяДляАнализа.ПомещениеФайлаОтменено);
		Сообщение.Сообщить();
		
	КонецЕсли;
	
	Если ТипЗнч(ПеременнаяДляАнализа) = Тип("Массив") Тогда
		
		Для каждого ЭлементКоллекции Из ПеременнаяДляАнализа Цикл
			
			ВывестиПараметрыПеременной(ЭлементКоллекции);
			
		КонецЦикла;	
		
	КонецЕсли;
	
КонецПроцедуры // ВывестиРезультатСообщением()

&НаКлиенте
Асинх Функция ВызватьОшибку()
	
	ВызватьИсключение "ошибка: слон не обнаружен!";	
	
КонецФункции

&НаКлиенте
Функция ВызватьОшибкуСинх()
	
	ВызватьИсключение "ошибка: слон не обнаружен!";	
	
КонецФункции

&НаКлиенте
Асинх Процедура ЗадатьВопросАсинх_ПриОткрытии()
	
	обещание = ВопросАсинх("Купить слона?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Результат = Ждать обещание; 
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		ЗадатьВопросАсинх_ПриОткрытии_Второй();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗадатьВопросАсинх_ПриОткрытии_Второй()
	
	обещание = ВопросАсинх("Купить мышку?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Результат = Ждать обещание; 
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		ЗадатьВопросАсинх_ПриОткрытии_Третий();
	Иначе
		ЭтаФорма.Закрыть();
	КонецЕсли;
	
КонецПроцедуры
 
&НаКлиенте
Асинх Процедура ЗадатьВопросАсинх_ПриОткрытии_Третий()
	
	обещание = ВопросАсинх("Напугать слона мышкой?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Результат = Ждать обещание; 
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Молодеца!'");;
		Сообщение.Сообщить();
	Иначе
		ЭтаФорма.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗадатьВопросАсинх_ПриЗакрытии()
	
	обещание = ВопросАсинх("Продать слона?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Результат = Ждать обещание; 
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		ЭтаФорма.Закрыть();
		ВопросПриЗакрытииЗадан = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

ВопросПриЗакрытииЗадан = Ложь;