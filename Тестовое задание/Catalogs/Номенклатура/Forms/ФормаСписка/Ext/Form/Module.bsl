#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаАналогов

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	СписокАналогов = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АналогиНоменклатуры.ГруппаАналогов КАК ГруппаАналогов
	|ПОМЕСТИТЬ ВТ_ГруппыАналогов
	|ИЗ
	|	РегистрСведений.АналогиНоменклатуры КАК АналогиНоменклатуры
	|ГДЕ
	|	АналогиНоменклатуры.Номенклатура В(&ГруппаАналогов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(АналогиНоменклатуры.Номенклатура) КАК НоменклатураПредставление,
	|	АналогиНоменклатуры.Номенклатура КАК Номенклатура,
	|	АналогиНоменклатуры.ГруппаАналогов КАК ГруппаАналогов
	|ИЗ
	|	РегистрСведений.АналогиНоменклатуры КАК АналогиНоменклатуры
	|ГДЕ
	|	АналогиНоменклатуры.ГруппаАналогов В
	|			(ВЫБРАТЬ
	|				ВТ_ГруппыАналогов.ГруппаАналогов
	|			ИЗ
	|				ВТ_ГруппыАналогов)
	|ИТОГИ ПО
	|	ГруппаАналогов
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("ГруппаАналогов", Строки.ПолучитьКлючи());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаГруппаАналогов = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаГруппаАналогов.Следующий() Цикл
		
		ВыборкаДетальныеЗаписи = ВыборкаГруппаАналогов.Выбрать();
		
		Аналоги = Новый СписокЗначений;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Аналоги.Добавить(ВыборкаДетальныеЗаписи.Номенклатура, ВыборкаДетальныеЗаписи.НоменклатураПредставление);	
		КонецЦикла;
		
		СписокАналогов.Вставить(ВыборкаГруппаАналогов.ГруппаАналогов, Аналоги);
		
	КонецЦикла;
	
	Для каждого Строка Из Строки Цикл
		
		ГруппаАналогов = Строка.Значение.Данные.ГруппаАналогов;
		Аналоги = СписокАналогов.Получить(ГруппаАналогов);
		Если Аналоги = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для каждого ЭлемСписка Из Аналоги Цикл
			
			Если ЭлемСписка.Значение <> Строка.Ключ Тогда
				
				Строка.Значение.Данные.СписокАналогов = Строка.Значение.Данные.СписокАналогов 
					+ ?(ЗначениеЗаполнено(Строка.Значение.Данные.СписокАналогов), Символы.ПС, "") + ЭлемСписка.Представление;	
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

