&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Поля = Новый Массив;
	Поля.Добавить("ГруппаАналогов");
	
	Список.УстановитьОграниченияИспользованияВГруппировке(Поля);
	Список.УстановитьОграниченияИспользованияВПорядке(Поля);
	Список.УстановитьОграниченияИспользованияВОтборе(Поля);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	текДанные = Элементы.Список.ТекущиеДанные;
	Если текДанные <> Неопределено Тогда
		УстановитьОтборПоАналогам();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоАналогам()

	ОтборыКомпоновкиДанных = АналогиНоменклатуры.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	
	ОтборПоГруппеРезерва = Неопределено;
	ОтборПоНоменклатуре = Неопределено;
	
	ПолеГруппаАналогов = Новый ПолеКомпоновкиДанных("ГруппаАналогов");
	ПолеНоменклатура = Новый ПолеКомпоновкиДанных("Номенклатура");
	
	Для Каждого Отбор из ОтборыКомпоновкиДанных Цикл
		
		Если ТипЗнч(Отбор) = Тип("ЭлементОтбораКомпоновкиДанных") И Отбор.ЛевоеЗначение = ПолеГруппаАналогов Тогда 
			ОтборПоГруппеРезерва = Отбор;
			//Прервать;
		КонецЕсли;
		
		Если ТипЗнч(Отбор) = Тип("ЭлементОтбораКомпоновкиДанных") И Отбор.ЛевоеЗначение = ПолеНоменклатура Тогда 
			ОтборПоНоменклатуре = Отбор;
			//Прервать;
		КонецЕсли;
		
	КонецЦикла;

	Если ОтборПоГруппеРезерва = Неопределено Тогда 
		ОтборПоГруппеРезерва = ОтборыКомпоновкиДанных.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборПоГруппеРезерва.ЛевоеЗначение = ПолеГруппаАналогов;
		ОтборПоГруппеРезерва.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборПоГруппеРезерва.Использование = Истина;
	КонецЕсли;
	
	Если ОтборПоНоменклатуре = Неопределено Тогда 
		ОтборПоНоменклатуре = ОтборыКомпоновкиДанных.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборПоНоменклатуре.ЛевоеЗначение = ПолеНоменклатура;
		ОтборПоНоменклатуре.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
		ОтборПоНоменклатуре.Использование = Истина;
	КонецЕсли;
	
	ОтборПоГруппеРезерва.ПравоеЗначение = Элементы.Список.ТекущиеДанные.ГруппаАналогов;
	ОтборПоГруппеРезерва.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОтборПоНоменклатуре.ПравоеЗначение = Элементы.Список.ТекущаяСтрока;
	ОтборПоНоменклатуре.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
КонецПроцедуры // УстановитьОтборПоАналогам()

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	СписокАналогов = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АналогиНоменклатуры.ГруппаАналогов КАК ГруппаАналогов,
	|	АналогиНоменклатуры.Номенклатура КАК Номенклатура
	|ИЗ
	|	РегистрСведений.АналогиНоменклатуры КАК АналогиНоменклатуры
	|ГДЕ
	|	АналогиНоменклатуры.Номенклатура В(&Номенклатура)";
	
	Запрос.УстановитьПараметр("Номенклатура", Строки.ПолучитьКлючи());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		СписокАналогов.Вставить(ВыборкаДетальныеЗаписи.Номенклатура, ВыборкаДетальныеЗаписи.ГруппаАналогов);
		
	КонецЦикла;
	
	Для каждого Строка Из Строки Цикл
		
		Если Строка.Значение.Данные.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		
		ГруппаАналогов = СписокАналогов.Получить(Строка.Ключ);
		Если ГруппаАналогов = Неопределено Тогда
			Продолжить;
		Иначе
			Строка.Значение.Данные.ГруппаАналогов = ГруппаАналогов;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

