#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Объект.Лево = "Я хороший текст";
	Объект.Право = "Я плохой текст";

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СравнитьЛевоПраво(Команда)

	Элементы.Результат.Документ.body.innerhtml = СравнитьЛевоПравоНаСервере(Объект.Лево, Объект.Право);
	
КонецПроцедуры  

&НаСервереБезКонтекста
Функция СравнитьЛевоПравоНаСервере(СтрЛево, СтрПраво)

	ТелоЗапроса      = ПодготовитьДанныеТелаЗапроса(СтрЛево, СтрПраво);	
	СтрокаДанные     = СконвертироватьТелоЗапросаВJSON(ТелоЗапроса);
	
	РезультатЗапроса = diffchecker_POST(СтрокаДанные);
	
	Возврат ОбработатьОтветdiffchecker_POST(РезультатЗапроса);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПодготовитьДанныеТелаЗапроса(СтрЛево, СтрПраво)
	
	ТелоЗапроса = Новый Соответствие;
	ТелоЗапроса.Вставить("left", СтрЛево);
	ТелоЗапроса.Вставить("right", СтрПраво);
	ТелоЗапроса.Вставить("diff_level", "word");
	
    Возврат ТелоЗапроса;

КонецФункции // ПодготовитьДанныеТелаЗапроса()

&НаСервереБезКонтекста
Функция СконвертироватьТелоЗапросаВJSON(ТелоЗапроса)
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, ТелоЗапроса);
	
    Возврат ЗаписьJSON.Закрыть();

КонецФункции // СконвертироватьТелоЗапросаВJSON()

&НаСервереБезКонтекста
Функция diffchecker_POST(СтрокаДанные)

	Сервер = "api.diffchecker.com";
	СтрокаЗапроса = "/public/text?output_type=html&email=YOUR_EMAIL";
	// вместо YOUR_EMAIL вставить почту регистрации на сервисе
	// output_type - описан на сайте, кратко: json, html и html_json
	
	ssl1 = Новый ЗащищенноеСоединениеOpenSSL();
	
	HTTPСоединение = Новый HTTPСоединение(Сервер,,,,,90,ssl1);
	HTTPЗапрос = Новый HTTPЗапрос(СтрокаЗапроса);
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/json");
	HTTPЗапрос.УстановитьТелоИзСтроки(СтрокаДанные, "UTF-8", ПредопределенноеЗначение("ИспользованиеByteOrderMark.НеИспользовать"));
	Попытка
		Результат = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);
	Исключение
		Текстсообщения = НСтр("ru='Произошла сетевая ошибка!'");
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Текстсообщения;
		Сообщение.Сообщить();
		
		Результат = Ложь;
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОбработатьОтветdiffchecker_POST(РезультатЗапроса)
	
	КодУспешногоОтветаHTTP = 200;
	Если РезультатЗапроса.КодСостояния <> КодУспешногоОтветаHTTP Тогда
		
		ДанныеОтвета = Неопределено;
		Текстсообщения = СтрШаблон(НСтр("ru='Ошибка соединения с сервисом! Код ошибки: %1'"), РезультатЗапроса.КодСостояния);
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Текстсообщения;
		Сообщение.Сообщить();
		
	Иначе
		
		ДанныеОтвета = РезультатЗапроса.ПолучитьТелоКакСтроку();
		
	КонецЕсли;
	
	Возврат ДанныеОтвета;
	
КонецФункции  

#КонецОбласти
