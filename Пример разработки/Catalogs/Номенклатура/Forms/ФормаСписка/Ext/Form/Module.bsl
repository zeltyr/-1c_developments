#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Поля = Новый Массив;
	Поля.Добавить("ГруппаАналогов");
	
	Список.УстановитьОграниченияИспользованияВГруппировке(Поля);
	Список.УстановитьОграниченияИспользованияВПорядке(Поля);
	Список.УстановитьОграниченияИспользованияВОтборе(Поля);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	СписокАналогов = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АналогиНоменклатуры.ГруппаАналогов КАК ГруппаАналогов,
	|	АналогиНоменклатуры.Номенклатура КАК Номенклатура
	|ИЗ
	|	РегистрСведений.АналогиНоменклатуры КАК АналогиНоменклатуры
	|ГДЕ
	|	АналогиНоменклатуры.Номенклатура В(&Номенклатура)";
	
	Запрос.УстановитьПараметр("Номенклатура", Строки.ПолучитьКлючи());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Для каждого Строка Из Строки Цикл
		
		Если Строка.Значение.Данные.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи.Сбросить();
		Если ВыборкаДетальныеЗаписи.НайтиСледующий(Строка.Ключ, "Номенклатура") Тогда
		
			Строка.Значение.Данные.ГруппаАналогов = ВыборкаДетальныеЗаписи.ГруппаАналогов;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	текДанные = Элементы.Список.ТекущиеДанные;
	Если текДанные <> Неопределено Тогда
		УстановитьОтборПоАналогам();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	УстановитьОтборПоАналогам();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыАналогиНоменклатуры

&НаКлиенте
Процедура АналогиНоменклатурыПослеУдаления(Элемент)
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура АналогиНоменклатурыПриИзменении(Элемент)
	Элементы.Список.Обновить();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьОтборПоАналогам()

	ОтборыКомпоновкиДанных = АналогиНоменклатуры.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	
	ОтборПоГруппеРезерва = Неопределено;
	ОтборПоНоменклатуре = Неопределено;
	
	ПолеГруппаАналогов = Новый ПолеКомпоновкиДанных("ГруппаАналогов");
	ПолеНоменклатура = Новый ПолеКомпоновкиДанных("Номенклатура");
	
	Для Каждого Отбор из ОтборыКомпоновкиДанных Цикл
		
		Если ТипЗнч(Отбор) = Тип("ЭлементОтбораКомпоновкиДанных") 
			И Отбор.ЛевоеЗначение = ПолеГруппаАналогов Тогда 
			ОтборПоГруппеРезерва = Отбор;
		КонецЕсли;
		
		Если ТипЗнч(Отбор) = Тип("ЭлементОтбораКомпоновкиДанных") 
			И Отбор.ЛевоеЗначение = ПолеНоменклатура Тогда 
			ОтборПоНоменклатуре = Отбор;
		КонецЕсли;
		
		Если ОтборПоНоменклатуре <> Неопределено 
			И ОтборПоГруппеРезерва <> Неопределено Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;

	Если ОтборПоГруппеРезерва = Неопределено Тогда 
		ОтборПоГруппеРезерва = ОтборыКомпоновкиДанных.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборПоГруппеРезерва.ЛевоеЗначение = ПолеГруппаАналогов;
		ОтборПоГруппеРезерва.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборПоГруппеРезерва.Использование = Истина;
	КонецЕсли;
	
	ОтборПоГруппеРезерва.ПравоеЗначение = Элементы.Список.ТекущиеДанные.ГруппаАналогов;
	ОтборПоГруппеРезерва.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	Если ОтборПоНоменклатуре = Неопределено Тогда 
		ОтборПоНоменклатуре = ОтборыКомпоновкиДанных.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборПоНоменклатуре.ЛевоеЗначение = ПолеНоменклатура;
		ОтборПоНоменклатуре.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
		ОтборПоНоменклатуре.Использование = Истина;
	КонецЕсли;
	
	ОтборПоНоменклатуре.ПравоеЗначение = Элементы.Список.ТекущаяСтрока;
	ОтборПоНоменклатуре.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
КонецПроцедуры // УстановитьОтборПоАналогам()

#КонецОбласти //СлужебныеПроцедурыИФункции


