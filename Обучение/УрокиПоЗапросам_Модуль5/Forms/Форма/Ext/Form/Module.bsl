#Область ОбработчикиКомандФормы

&НаСервере
Процедура ПостроитьКроссТаблицуНаСервере(ТабДок)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПродажиКомпанииОбороты.Номенклатура КАК Товар,
	|	ПРЕДСТАВЛЕНИЕ(ПродажиКомпанииОбороты.Номенклатура) КАК ТоварПредставление,
	|	ПродажиКомпанииОбороты.Контрагент КАК Покупатель,
	|	ПРЕДСТАВЛЕНИЕ(ПродажиКомпанииОбороты.Контрагент) КАК ПокупательПредставление,
	|	ПродажиКомпанииОбороты.СуммаОборот КАК Сумма
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты КАК ПродажиКомпанииОбороты
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	ОБЩИЕ,
	|	Покупатель,
	|	Товар";
	
	
	Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("КроссТаблица");
	
	ОбластьШапкаТовар = Макет.ПолучитьОбласть("Шапка|Товар");
	ОбластьШапкаПокупатель = Макет.ПолучитьОбласть("Шапка|Покупатель");
	ОбластьШапкаИтог = Макет.ПолучитьОбласть("Шапка|Итог");
	
	ОбластьСтрокаТовар = Макет.ПолучитьОбласть("Строка|Товар");
	ОбластьСтрокаПокупатель = Макет.ПолучитьОбласть("Строка|Покупатель");
	ОбластьСтрокаИтог = Макет.ПолучитьОбласть("Строка|Итог");
	
	ОбластьПодвалТовар = Макет.ПолучитьОбласть("Подвал|Товар");
	ОбластьПодвалПокупатель = Макет.ПолучитьОбласть("Подвал|Покупатель");
	ОбластьПодвалИтог = Макет.ПолучитьОбласть("Подвал|Итог");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаКлиент = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Покупатель");
	ТабДок.Вывести(ОбластьШапкаТовар);
	
	Пока ВыборкаКлиент.Следующий() Цикл
		
		ОбластьШапкаПокупатель.Параметры.Заполнить(ВыборкаКлиент);
		ТабДок.Присоединить(ОбластьШапкаПокупатель);
		
		
	КонецЦикла;
	ТабДок.Присоединить(ОбластьШапкаИтог);
	
	ВыборкаТовар = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Товар");
	
	Пока ВыборкаТовар.Следующий() Цикл
		
		ОбластьСтрокаТовар.Параметры.Заполнить(ВыборкаТовар);
		ТабДок.Вывести(ОбластьСтрокаТовар);
		
		ВыборкаПокупатель = ВыборкаТовар.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Покупатель", "ВСЕ");
		Пока ВыборкаПокупатель.Следующий() Цикл
			
			ОбластьСтрокаПокупатель.Параметры.Заполнить(ВыборкаПокупатель);
			ТабДок.Присоединить(ОбластьСтрокаПокупатель);
			
		КонецЦикла;
		
		ОбластьСтрокаИтог.Параметры.Заполнить(ВыборкаТовар);
		ТабДок.Присоединить(ОбластьСтрокаИтог);
		
	КонецЦикла;
	
	ТабДок.Вывести(ОбластьПодвалТовар);
	ВыборкаКлиент.Сбросить();
	Пока ВыборкаКлиент.Следующий() Цикл
		
		ОбластьПодвалПокупатель.Параметры.Заполнить(ВыборкаКлиент);
		ТабДок.Присоединить(ОбластьПодвалПокупатель);
		
		
	КонецЦикла;
	
	ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если ВыборкаОбщийИтог.Следующий() Тогда
		
		ОбластьПодвалИтог.Параметры.Заполнить(ВыборкаОбщийИтог);
		ТабДок.Присоединить(ОбластьПодвалИтог);
		
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПостроитьКроссТаблицу(Команда)
	ТабДок = Новый ТабличныйДокумент;
	ПостроитьКроссТаблицуНаСервере(ТабДок);
	ТабДок.Показать();
КонецПроцедуры


&НаСервере
Процедура ВыводИтоговСПериодамиНаСервере(ТабДок)
	
	Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("ДатаПериодами");
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажиОбороты.Период КАК Период,
		|	ПродажиОбороты.Контрагент КАК Контрагент,
		|	ПРЕДСТАВЛЕНИЕ(ПродажиОбороты.Контрагент) КАК КонтрагентПредставление,
		|	ПродажиОбороты.Номенклатура КАК Номенклатура,
		|	ПРЕДСТАВЛЕНИЕ(ПродажиОбороты.Номенклатура) КАК НоменклатураПредставление,
		|	ПродажиОбороты.СуммаОборот КАК Сумма
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(, , День, ) КАК ПродажиОбороты
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период
		|ИТОГИ ПО
		|	Период ПЕРИОДАМИ(ДЕНЬ, , )";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьПодвалТаблицы = Макет.ПолучитьОбласть("ПодвалТаблицы");
	ОбластьПериод = Макет.ПолучитьОбласть("Период");
	ОбластьДетальныхЗаписей = Макет.ПолучитьОбласть("Детали");
	
	ТабДок.Очистить();
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапкаТаблицы);
	ТабДок.НачатьАвтогруппировкуСтрок();
	
	ВыборкаПериод = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "ВСЕ");
	
	Пока ВыборкаПериод.Следующий() Цикл
		ОбластьПериод.Параметры.Заполнить(ВыборкаПериод);
		ТабДок.Вывести(ОбластьПериод, ВыборкаПериод.Уровень());
	
		ВыборкаДетальныеЗаписи = ВыборкаПериод.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ОбластьДетальныхЗаписей.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
			ТабДок.Вывести(ОбластьДетальныхЗаписей, ВыборкаДетальныеЗаписи.Уровень());
		КонецЦикла;
	КонецЦикла;
	
	ТабДок.ЗакончитьАвтогруппировкуСтрок();
	ТабДок.Вывести(ОбластьПодвалТаблицы);
	ТабДок.Вывести(ОбластьПодвал);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводИтоговСПериодами(Команда)
	
	ТабДок = Новый ТабличныйДокумент;
	ВыводИтоговСПериодамиНаСервере(ТабДок);
	ТабДок.Показать();
	
КонецПроцедуры


&НаСервере
Процедура ВывестиДиаграммуНаСервере(Диаграмма)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровТовары.Номенклатура КАК Номенклатура,
		|	ПРЕДСТАВЛЕНИЕ(РеализацияТоваровТовары.Номенклатура) КАК НоменклатураПредставление,
		|	РеализацияТоваровТовары.Ссылка.Контрагент КАК Контрагент,
		|	ПРЕДСТАВЛЕНИЕ(РеализацияТоваровТовары.Ссылка.Контрагент) КАК КонтрагентПредставление,
		|	РеализацияТоваровТовары.Сумма КАК Сумма
		|ИЗ
		|	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
		|ИТОГИ
		|	СУММА(Сумма)
		|ПО
		|	Номенклатура,
		|	Контрагент";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	Диаграмма.АвтоТранспонирование = Ложь;
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Серия = Диаграмма.УстановитьСерию(ВыборкаНоменклатура.Номенклатура);
		Серия.Текст = ВыборкаНоменклатура.НоменклатураПредставление;
		Серия.Расшифровка = ВыборкаНоменклатура.Номенклатура;
	
		ВыборкаКонтрагент = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
		Пока ВыборкаКонтрагент.Следующий() Цикл
			Точка = Диаграмма.УстановитьТочку(ВыборкаКонтрагент.Контрагент);
			Точка.Текст = ВыборкаКонтрагент.КонтрагентПредставление;
			Точка.Расшифровка = ВыборкаКонтрагент.Контрагент;
			Диаграмма.УстановитьЗначение(Точка, Серия, ВыборкаКонтрагент.Сумма);
		КонецЦикла;
	КонецЦикла;
	
	Диаграмма.АвтоТранспонирование = Истина;
	Диаграмма.Обновление = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ВывестиДиаграмму(Команда)
	ВывестиДиаграммуНаСервере(Диаграмма);
КонецПроцедуры

#КонецОбласти


